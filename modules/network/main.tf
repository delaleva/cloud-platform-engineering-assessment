# A private VPC with no internet gateway, no NAT and no public subnets.
resource "aws_vpc" "this" {
  cidr_block = var.vpc_cidr

  # Interface endpoint private DNS does not resolve without hostnames enabled.
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = "${var.name}-vpc"
  }
}

resource "aws_subnet" "private" {
  for_each = { for idx, az in var.azs : az => idx }

  vpc_id                  = aws_vpc.this.id
  availability_zone       = each.key
  cidr_block              = cidrsubnet(var.vpc_cidr, 4, each.value)
  map_public_ip_on_launch = false

  tags = {
    Name = "${var.name}-private-${each.key}"
  }
}

# This table carries no default route, so nothing in these subnets has a path
# to the internet.
resource "aws_route_table" "private" {
  vpc_id = aws_vpc.this.id

  tags = {
    Name = "${var.name}-private"
  }
}

resource "aws_route_table_association" "private" {
  for_each = aws_subnet.private

  subnet_id      = each.value.id
  route_table_id = aws_route_table.private.id
}

# The default network ACL allows all traffic in both directions, and is left
# that way. Nothing outside the VPC can reach these subnets, and the security
# groups already control every path inside it.
